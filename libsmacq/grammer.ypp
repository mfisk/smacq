/*
 * BUGS:
 *
 *    - Cannot call the same function twice with different "as" names
 *      This is because renames are done as a batch just before the VERB
 *
 *    - Cannot use nested functions as arguments
 *      FIX: re-write arguments recursively or something
 *
 */

/*
 * Examples:
 *
 *	print srcip from (uniq srcip, dstip from pcapfile("/hog/traces/aa.80")) group by srcip having "counter>10"
 *	print srcip, counter(), sum(len) from (uniq srcip, dstip from pcapfile("/hog/traces/aa.80")) group by srcip having "counter>10"
 *
 */
 
%{
#include <smacq-parser.h>
%}

%token YYDEFINE
%token BY
%token SELECT
%token AS
%token HAVING
%token UNTIL
%token YYSTRING YYID YYNUMBER YYCAPID
%token YYNEQ YYLEQ YYGEQ
%token YYSTOP 

%left YYAND YYOR
%right YYNOT

%left '+' '-'
%left '*' '/'

%left FROM
%right '|'
%token UNION

%right WHERE
%left GROUP

%type <arglist> arg argument args0 args commaargs morecommaargs spacedargs spacedargs0
%type <string> word string id number
%type <op> op
%type <comp> boolean test 
%type <operand> operand expression subexpression
%type <arithop> arithop

%union {
  SmacqGraph * graph;
  struct arglist * arglist;
  struct vphrase vphrase;
  char * string;
  struct group group;
  dts_compare_operation op;
  enum dts_arith_operand_type arithop;
  dts_comparison * comp;
  struct dts_operand * operand;
  struct reflist_element ref;
  TermList * termlist;
  struct joinlist * join_list;
  DatalogNamespace * nmspace;
}
%%

null:   /* empty */ ;


word:	id 		
	| string
	| number 
	;

number:	YYNUMBER 	{ $$ = yystring; }
	;

string:	YYSTRING 	{ $$ = yystring; }
	;

id:	YYID 		{ $$ = yystring; }
	| YYCAPID 	{ $$ = yystring; }
        | YYOR          { $$ = "or"; }
        | YYAND         { $$ = "and"; }
        ;

arg: argument 
	| argument AS word		{ $$->rename = $3; }
	;

argument : 
	'-'  			{ $$ = newarg("-", WORD, NULL); } 
	| word 			{ $$ = newarg($1, WORD, NULL); } 
	| id '(' args0 ')'	{ $$ = newarg($1, FUNCTION, $3); }
	| '[' expression ']'	{ $$ = newarg("expr", FUNCTION, 
					       newarg(print_operand($2), WORD, NULL)); 
			                  }
	;

args0 : args 
	| null { $$ = NULL; };

args :  commaargs
	| spacedargs	
	;

commaargs: 
	arg ',' arg morecommaargs { $$ = $1; $$->next = $3; $3->next = $4; }	
	;

spacedargs0 : 
	null 			{ $$ = NULL; }
	| spacedargs
	;

spacedargs : 
	arg spacedargs 		{ $$ = $1; $$->next = $2; }
	| arg null 		{ $$ = $1; $$->next = NULL; }
	;

morecommaargs : null		{ $$ = NULL; }
	| ',' arg morecommaargs { $$ = $2; $$->next = $3; }
	;



/************* From boolean parser: **********************/


boolean : '(' boolean ')'	{ $$ = $2; }
	| boolean YYOR boolean	{ $$ = comp_join($1, $3, OR); }
	| boolean YYAND boolean	{ $$ = comp_join($1, $3, AND); }
	| YYNOT boolean 	{ $$ = comp_join($2, NULL, NOT); }
	| test 		
	;

operand : id			{ $$ = comp_operand(FIELD, $1); }
	| string 		{ $$ = comp_operand(CONST, $1); }
	| number 		{ $$ = comp_operand(CONST, $1); }
	;


expression :   
	'(' expression ')' 
				{	  
				  $$ = $2;  
				} 
	| subexpression arithop subexpression  
				{
				  $$ = parse_dts->comp_arith($2, $1, $3); 
				}
	;

subexpression : 
	expression
	| operand
	;

test : 	
	operand		{  $$ = comp_new(EXIST, $1, $1); }
	| subexpression op subexpression      { $$ = comp_new($2, $1, $3); }
	| id '(' args0 ')'	{ 
				  int argc; char ** argv;
				  arglist2argv($3, &argc, &argv);
				  $$ = comp_new_func($1, argc, argv, $3);
				}
	;

op : '=' 		{ $$ = EQ; }
	| '>'		{ $$ = GT; }
	| '<' 		{ $$ = LT; }
	| YYGEQ		{ $$ = GEQ; }
	| YYLEQ		{ $$ = LEQ; }
	| YYNEQ		{ $$ = NEQ; }
	;

arithop : '+'		{ $$ = ADD; }
	| '-'		{ $$ = SUB; }
	| '/'		{ $$ = DIVIDE; }
	| '*'		{ $$ = MULT; }
	;
